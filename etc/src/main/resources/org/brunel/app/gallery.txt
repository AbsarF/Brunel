#id             bggbubble
#title          Bubble chart of 2000 games
#description    A simple bubble chart showing the top ranked games.
                The color shows the BGG rating and the size of each bubble represents the number of
                voters for that game. The data is already sorted by rank, so no sort was needed.
                Data is from March 2015.
#brunel         data('http://brunel.mybluemix.net/sample_data/BGG%20Top%202000%20Games.csv')
                bubble color(rating) size(voters) sort(rating) label(title) tooltip(title, #all)
                legends(none) style('* {font-size: 7pt}')


#id             bggtreemap
#title          Treemap of game designers and mechanics
#description    The treemap sizes are calculated by summing the votes for each category, and the color is the mean
                rating for games in those categories. Tooltips list the titles, and labels give the earliest date
                for any game in that category.
                Data is from March 2015.
#brunel         data('http://brunel.mybluemix.net/sample_data/BGG%20Top%202000%20Games.csv')
                treemap x(designer, mechanics) color(rating) size(voters) sort(#count) label(published)
                tooltip(#all, title) sum(voters) mean(rating) min(published) list(title:50)

#id             bggchord
#title          Chord chart for game player age
#description    The chord chart shows which categories of games are rated as suitable for given ages of
                players. These age ratings are given by the BGG reviewers and are the median responses
                for each game
#width          600
#height         600
#brunel         data('http://brunel.mybluemix.net/sample_data/BGG%20Top%202000%20Games.csv')
                chord x(categories) y(playerage) color(playerage) size(#count) tooltip(#all)

#id             bggpub
#title          When top games were published
#description    The main path element shows, by size, the number of games published each year, with
                points superimposed to show the top 20 games, with tooltips. The data has been trimmed
                to remove the earliest 100 games, as they are from the early parts fo the century.
#width          800
#height         500
#brunel         data('http://brunel.mybluemix.net/sample_data/BGG%20Top%202000%20Games.csv')
                path x(published) y(categories) color(categories) size(voters:200) top(published:1800) sum(voters) legends(none)
                + data('http://brunel.mybluemix.net/sample_data/BGG%20Top%202000%20Games.csv')
                x(published) y(categories) size(voters:300) tooltip('#', rank, ': ', title) top(published:1800, rating:20)

#id             statesbar
#title          State temperatures
#height         400
#description    Differences between winter and summer temperatures, sorted by the latitude of the state.
                Color encodes the state population.
#brunel         data('http://brunel.mybluemix.net/sample_data/US%20States.csv')
                bar x(state) yrange(summer, winter) color(population) sort(latitude)

#id             statestext
#title          Text Scatterplot of state info
#description    Areas and Populations for US states, colored by the percentage difference in votes between
                democrat and republican in the 2012 election.
#brunel         data('http://brunel.mybluemix.net/sample_data/US%20States.csv')
                text x(area) y(population) color(dem_rep:red-blue) label(abbr) tooltip(#all) style('font-size:36px;font-family:times')

#id             states2plots
#ext            gif
#title          Interactive linked state charts
#description    Linking from a heatmap of state data to a bubble plot of the states themselves
#brunel         data('http://brunel.mybluemix.net/sample_data/US%20States.csv')
                x(Income) y(Violent_Crimes) color(#count:blue) label(#selection) bin(violent_crimes, income)
                legends(none) style('.label {font-size:40px}') interaction(select)
                | data('http://brunel.mybluemix.net/sample_data/US%20States.csv')
                bubble color(violent_crimes:red)
                size(population) sort(population) label(abbr) interaction(filter)

#id             airlinesDelay
#title          Airline delays
#description    Data from an ASA competition showing a summary of which days are the worst to travel on. Note that
                the time period 9/11 - 9/15 are outliers due to the impact of cancellations in 2001.
#brunel         data('http://brunel.mybluemix.net/sample_data/AirlineDelays.csv')
                text x(month) y(day) color(count:red) size(count:200) label(month, '/', day) top(result:1) sum(count) axes(none)

#id             airlinesDelay2
#title          Airline Overview
#description    Data from an ASA competition showing a summary of which days are the worst to travel on. This
                chart shows what happens on a per-month basis
#brunel         data('http://brunel.mybluemix.net/sample_data/AirlineDelays.csv')
                area x(month) y(Count) stack color(result) percent(Count)

#id             whiskey1
#title          Whiskey Categories and Countries [1]
#description    Whiskey data as a simple chart
#brunel         data('http://brunel.mybluemix.net/sample_data/whiskey.csv')
                x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             whiskey2
#title          Whiskey Categories and Countries [2]
#description    Adding 'chord' to brunel -- no other changes needed to make this a chord diagram
#brunel         data('http://brunel.mybluemix.net/sample_data/whiskey.csv')
                chord x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             whiskey3
#title          Whiskey Categories and Countries [3]
#description    Adding 'treemap' to brunel -- no other changes needed to make this a treemap diagram
#brunel         data('http://brunel.mybluemix.net/sample_data/whiskey.csv')
                treemap x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             whiskey4
#title          Whiskey Categories and Countries [4]
#description    Adding 'bubble' to brunel -- no other changes needed to make this a bubble diagram
#brunel         data('http://brunel.mybluemix.net/sample_data/whiskey.csv')
                bubble x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             minard1
#title          Minard's famous map of Napoleon's advance on Moscow
#description    This graph shows the catastrophic loss of life in Napoleon's Grand Army as it advanced on, and then
                retreated from Russia.  The size of the army, initially 422,000 strong, is shown by the width of the path.
                Below it, aligned with the main chart, is a temperature chart showing the increasingly brutal cold which
                followed the soldiers' retreat.
#width          1000
#height         450
#brunel         data('http://brunel.mybluemix.net/sample_data/minard-troops.csv')
                path x(long) y(lat) color(direction) size(survivors:0:1250) split(group) axes(none) legends(none)
                at(-10, 0, 112, 80) style('stroke:none; fill-opacity:1')
                + data('http://brunel.mybluemix.net/sample_data/minard-cities.csv')
                text x(long) y(lat) label(city) style('fill:black;font-family:Times; font-size:12pt')
                +  data('http://brunel.mybluemix.net/sample_data/minard-troops.csv')  size(survivors:0:1250)
                x(long) y(lat) style('fill-opacity:0;stroke-opacity:0') tooltip(group, survivors, direction)
                | data('http://brunel.mybluemix.net/sample_data/minard-temp.csv')
                line x(long) y(temp) at(0, 60, 110, 100) style('stroke:green')
                + data('http://brunel.mybluemix.net/sample_data/minard-temp.csv')
                text x(long) y(temp) label(temp, 'Â°C ', month, ' ', day)
                style('font-family: Times; font-style:italic; fill:black')

#id             bigtenwins
#title          Big Ten Football Wins
#description    The bubble chart shows the total number of wins for each Big Ten team since 1950, partitioned by the current East/West divisions.
				Click on a team to see the history of wins per each year as they compare to the rest of the Big Ten teams over time as well as
				a plot of the number of points allowed vs. the number of points scored for each year--which also includes a fit line to show the trend.
#ext            gif
#width          1000
#height         900
#brunel         data('http://brunel.mybluemix.net/sample_data/BigTenWins.csv')
                bubble x(Division, Team) size(Win) color(Team) label(Team)  sum(Win) tooltip(#all) legends(none) interaction(select) at(0,-12,60,60) |
                data('http://brunel.mybluemix.net/sample_data/BigTenWins.csv')
                x(Points_Allowed) y(Points_Scored) interaction(filter) tooltip(Year) at(60,10,100,45) +
                data('http://brunel.mybluemix.net/sample_data/BigTenWins.csv')
                x(Points_Allowed) y(Points_Scored) interaction(filter)  line fit(Points_Scored)  |
                data('http://brunel.mybluemix.net/sample_data/BigTenWins.csv')
                x(Year) y(Win) area stack split(Team) opacity(#selection) style('stroke-width:.5') at(0,50,100,100)

#id             div1aconf
#title          NCAA Division 1-A Conference Wins
#description    The bar chart shows the total number of wins each conference has against opposing conferences since 1978.  Clicking on a bar
  				shows a chord chart of victories for the selected conference against opponents in other conferences.  Darker blue indicates a greater number of
  				victories.  The bottom two charts show the spread of the point differentials (difference between final game scores) when the selected conference is
  				the home vs. the away team.
#width          1000
#height         600
#brunel         data('http://brunel.mybluemix.net/sample_data/Conferences.csv')
                x(Wins) y(#count:linear) bar  transpose sort(#count: ascending) tooltip(#all) interaction(select) at (0,0,45,50) |
                data('http://brunel.mybluemix.net/sample_data/Conferences.csv')
                chord x(As_Home) y(As_Visitor) color(#count) interaction(filter) tooltip(#all) at (45,0,100,60) |
                data('http://brunel.mybluemix.net/sample_data/Conferences.csv')
                x(As_Home) yrange(Score_Difference:linear) iqr(Score_Difference) bar interaction(filter) at(0,60,50,100) tooltip(#all) +
                data('http://brunel.mybluemix.net/sample_data/Conferences.csv')
                x(As_Home) y(Score_Difference:linear) median(Score_Difference) point style('symbol:rect;border-radius:5') interaction(filter)  tooltip(#all)  |
                data('http://brunel.mybluemix.net/sample_data/Conferences.csv')
                x(As_Visitor) yrange(Score_Difference:linear) bar interaction(filter) iqr(Score_Difference)  tooltip(#all) at(50,60,100,100) +
                data('http://brunel.mybluemix.net/sample_data/Conferences.csv')
                x(As_Visitor) y(Score_Difference:linear) median(Score_Difference) point style('symbol:rect;border-radius:5') interaction(filter)  tooltip(#all)

