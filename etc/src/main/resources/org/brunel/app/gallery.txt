#id             bggbubble
#title          Bubble chart of 2000 games
#description    A simple bubble chart showing the top ranked games.
                The color shows the BGG rating and the size of each bubble represents the number of
                voters for that game. The data is already sorted by rank, so no sort was needed.
                Data is from March 2015.
#brunel         data('http://willsfamily.org/files/vis/data/BGG Top 2000 Games.csv')
                bubble color(rating) size(voters) sort(rating) label(title) tooltip(title, #all)
                legends(none) style('* {font-size: 7pt}')


#id             bggtreemap
#title          Treemap of game designers and mechanics
#description    The treemap sizes are calculated by summing the votes for each category, and the color is the mean
                rating for games in those categories. Tooltips list the titles, and labels give the earliest date
                for any game in that category.
                Data is from March 2015.
#brunel         data('http://willsfamily.org/files/vis/data/BGG Top 2000 Games.csv')
                treemap x(designer, mechanics) color(rating) size(voters) sort(#count) label(published)
                tooltip(#all, title) sum(voters) mean(rating) min(published) list(title:50)

#id             bggchord
#title          Chord chart for game player age
#description    The chord chart shows which categories of games are rated as suitable for given ages of
                players. These age ratings are given by the BGG reviewers and are the median responses
                for each game
#width          600
#height         600
#brunel         data('http://willsfamily.org/files/vis/data/BGG Top 2000 Games.csv')
                chord x(categories) y(playerage) color(playerage) size(#count) tooltip(#all)

#id             statesbar
#title          State temperatures
#height         400
#description    Differences between winter and summer temperatures, sorted by the latitude of the state.
                Color encodes the state population.
#brunel         data('http://willsfamily.org/files/vis/data/US States.csv')
                bar x(state) yrange(summer, winter) color(population) sort(latitude)

#id             statestext
#title          Text Scatterplot of state info
#description    Areas and Populations for US states, colored by the percentage difference in votes between
                democrat and republican in the 2012 election.
#brunel         data('http://willsfamily.org/files/vis/data/US States.csv')
                text x(area) y(population) color(dem_rep:red-blue) label(abbr) tooltip(#all) style('font-size:36px;font-family:times')

#id             states2plots
#ext            gif
#title          Interactive linked state charts
#description    Linking from a heatmap of state data to a bubble plot of the states themselves
#brunel         data('http://willsfamily.org/files/vis/data/US States.csv')
                x(Income) y(Violent_Crimes) color(#count:blue) label(#selection) bin(violent_crimes, income)
                legends(none) style('.label {font-size:40px}') interaction(select)
                | data('http://willsfamily.org/files/vis/data/US States.csv')
                bubble color(violent_crimes:red)
                size(population) sort(population) label(abbr) interaction(filter)

#id             airlinesDelay
#title          Airline delays
#description    Data from an ASA competition showing a summary of which days are the worst to travel on. Note that
                the time period 9/11 - 9/15 are outliers due to the impact of cancellations in 2001.
#brunel         data('http://willsfamily.org/files/vis/data/AirlineDelays.csv')
                text x(month) y(day) color(count:red) size(count:200) label(month, '/', day) top(result:1) sum(count) axes(none)

#id             airlinesDelay2
#title          Airline Overview
#description    Data from an ASA competition showing a summary of which days are the worst to travel on. This
                chart shows what happens on a per-month basis
#brunel         data('http://willsfamily.org/files/vis/data/AirlineDelays.csv')
                area x(month) y(Count) stack color(result) percent(Count)

#id             whiskey1
#title          Whiskey Categories and Countries [1]
#description    Whiskey data as a simple chart
#brunel         data('http://willsfamily.org/files/vis/data/whiskey.csv')
                x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             whiskey2
#title          Whiskey Categories and Countries [2]
#description    Adding 'chord' to brunel -- no other changes needed to make this a chord diagram
#brunel         data('http://willsfamily.org/files/vis/data/whiskey.csv')
                chord x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             whiskey3
#title          Whiskey Categories and Countries [3]
#description    Adding 'treemap' to brunel -- no other changes needed to make this a treemap diagram
#brunel         data('http://willsfamily.org/files/vis/data/whiskey.csv')
                treemap x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             whiskey4
#title          Whiskey Categories and Countries [4]
#description    Adding 'bubble' to brunel -- no other changes needed to make this a bubble diagram
#brunel         data('http://willsfamily.org/files/vis/data/whiskey.csv')
                bubble x(country) y(category) color(country) size(#count) sort(#count) label(category) tooltip(#all)

#id             minard1
#title          Minard's famous map of Napoleon's advance on Moscow
#description    This graph shows the catastrophic loss of life in Napoleon's Grand Army as it advanced on, and then
                retreated from Russia.  The size of the army, initially 422,000 strong, is shown by the width of the path.
                Below it, aligned with the main chart, is a temperature chart showing the increasingly brutal cold which
                followed the soldiers' retreat.
#width          1000
#height         450
#brunel         data('http://willsfamily.org/files/vis/data/minard-troops.csv')
                path x(long) y(lat) color(direction) size(survivors:0:1250) split(group) axes(none) legends(none)
                at(-10, 0, 112, 80) style('stroke:none; fill-opacity:1')
                + data('http://willsfamily.org/files/vis/data/minard-cities.csv')
                text x(long) y(lat) label(city) style('fill:black;font-family:Times; font-size:12pt')
                +  data('http://willsfamily.org/files/vis/data/minard-troops.csv')  size(survivors:0:1250)
                x(long) y(lat) style('fill-opacity:0;stroke-opacity:0') tooltip(group, survivors, direction)
                | data('http://willsfamily.org/files/vis/data/minard-temp.csv')
                line x(long) y(temp) at(0, 60, 110, 100) style('stroke:green')
                + data('http://willsfamily.org/files/vis/data/minard-temp.csv')
                text x(long) y(temp) label(temp, 'Â°C ', month, ' ', day)
                style('font-family: Times; font-style:italic; fill:black')
